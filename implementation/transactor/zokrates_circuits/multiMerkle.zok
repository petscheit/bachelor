import "hashes/sha256/512bitPadded" as sha256
import "hashes/sha256/1024bitPadded" as sha256_4
import "utils/casts/u32_to_field" as to_field
import "utils/pack/u32/nonStrictUnpack256" as to_u32_8

struct Trade {
        field ethAmount 
        field tokenAmount 
        field nonce
        field deltaEth
        field deltaToken
        field direction // 0: -eth, +token  1: -token, +eth
}

struct Balance {
        field ethAmount
        field tokenAmount
        field nonce
}

def hashPair(u32[8] a, u32[8] b) -> (u32[8]):
	return if to_field(a[0]) <= to_field(b[0]) then sha256(a, b) else sha256(b, a) fi


def hashLeaves(Trade[3] trades) -> (u32[3][8]):
	u32[3][8] res = [[0x00000000;8];3]
	u32[8] padding = [0x00000000;8]
	for field i in 0..3 do
		u32[8] temp = sha256_4(to_u32_8(trades[i].ethAmount), to_u32_8(trades[i].tokenAmount), to_u32_8(trades[i].nonce), padding)
		res[i] = temp
	endfor
	return res

def getPosition(bool proofFlag, field leafPos, field leafsLen) -> (field):
	return if proofFlag then if leafPos < leafsLen then 0 else 1 fi else 2 fi
	
def verifyMultiProof(u32[9][8] leafs, u32[9][8] proofs, bool[9] proofFlags) -> (u32[8]):
	field leafsLen = 3
	field totalHashes = 9
	u32[9][8] hashes = [[0x00000000;8];9]
	field[3] indexes = [0, 0, 0] // 0: leafPos, 1: hashPos, 2: proofPos
	for field i in 0..totalHashes do
		u32[3][8] nodes = [leafs[indexes[0]], hashes[indexes[1]], proofs[indexes[2]]]
		field aPos = getPosition(proofFlags[i], indexes[0], leafsLen)
		u32[8] a = nodes[aPos]
		indexes[aPos] = indexes[aPos] + 1

		nodes = [leafs[indexes[0]], hashes[indexes[1]], proofs[indexes[2]]]
		field bPos = getPosition(true, indexes[0], leafsLen)
		u32[8] b = nodes[bPos]
		indexes[bPos] = indexes[bPos] + 1
		u32[8] hash = hashPair(a, b)
		hashes[i] = hash
	endfor
	return hashes[totalHashes - 1]

	
def main(Trade[3] trades, private u32[9][8] proof, private bool[9] proofFlags) -> (u32[8]):
	u32[9][8] leafsPadded = [...hashLeaves(trades), ...[[0x00000000;8];6]]

	return verifyMultiProof(leafsPadded, proof, proofFlags)
